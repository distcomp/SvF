
SELECT: t, x1, y1, x2, y2, x3, y3 FROM "3Body2D.dat" as DB

SET: t = [ , 20, 0.01]  # временная ось

# Положения планет как функции времени (2D)
VAR: 	x1(t)  # планета 1, координата x
 	y1(t)  # планета 1, координата y
 	x2(t)  # планета 2, координата x
	y2(t)  # планета 2, координата y
 	x3(t)  # планета 3, координата x
	y3(t)  # планета 3, координата y

# Искомые параметры
# VAR: G_const; G_const > 0  # гравитационная постоянная
G_const = 1  # нормированная гравитационная постоянная
VAR: m1; m1 > 0  # масса планеты 1
VAR: m2; m2 > 0  # масса планеты 2
VAR: m3; m3 > 0  # масса планеты 3

# Уравнения движения (закон всемирного тяготения в 2D)
# Для планеты 1
EQ: x1'' = G_const * m2 * (x2 - x1) / ((x2 - x1)^2 + (y2 - y1)^2)^(3/2) + \
           G_const * m3 * (x3 - x1) / ((x3 - x1)^2 + (y3 - y1)^2)^(3/2)

EQ: y1'' = G_const * m2 * (y2 - y1) / ((x2 - x1)^2 + (y2 - y1)^2)^(3/2) + \
           G_const * m3 * (y3 - y1) / ((x3 - x1)^2 + (y3 - y1)^2)^(3/2)

# Для планеты 2
EQ: x2'' = G_const * m1 * (x1 - x2) / ((x1 - x2)^2 + (y1 - y2)^2)^(3/2) + \
           G_const * m3 * (x3 - x2) / ((x3 - x2)^2 + (y3 - y2)^2)^(3/2)

EQ: y2'' = G_const * m1 * (y1 - y2) / ((x1 - x2)^2 + (y1 - y2)^2)^(3/2) + \
           G_const * m3 * (y3 - y2) / ((x3 - x2)^2 + (y3 - y2)^2)^(3/2)

# Для планеты 3  
EQ: x3'' = G_const * m1 * (x1 - x3) / ((x1 - x3)^2 + (y1 - y3)^2)^(3/2) + \
           G_const * m2 * (x2 - x3) / ((x2 - x3)^2 + (y2 - y3)^2)^(3/2)

EQ: y3'' = G_const * m1 * (y1 - y3) / ((x1 - x3)^2 + (y1 - y3)^2)^(3/2) + \
           G_const * m2 * (y2 - y3) / ((x2 - x3)^2 + (y2 - y3)^2)^(3/2)

CV: 	NumOfSets=5
RUN: 	MaxIter=1

# Критерий: близость траекторий + сложность траекторий
OBJ: x1.MSD() + y1.MSD() + \
     x2.MSD() + y2.MSD() + \
     x3.MSD() + y3.MSD() + \
     x1.Complexity(Penal[0]) + y1.Complexity(Penal[1]) + \
     x2.Complexity(Penal[2]) + y2.Complexity(Penal[3]) + \
     x3.Complexity(Penal[4]) + y3.Complexity(Penal[5])

#PLOT: 	DB.x1,DB.y1, lw=0,c=gray,marker=o,ms=3,label=Date  & DB.x2,DB.y2, label=""  &  DB.x3,DB.y3  \
 #     & x1.grd,y1.grd, c=red,ms=0,lw=1,label=Alpha  & x2.grd,y2.grd, c=b,label=Betta  & x3.grd,y3.grd, c=green,label=Gamma

PLOT: 	DB.x1,DB.y1, lw=0,c=gray,marker='o',ms=3,label=Date  + DB.x2,DB.y2, label=""  +  DB.x3,DB.y3  \
      + x1.grd,y1.grd, c=red,ms=0,lw=1,label=Alpha  + x2.grd,y2.grd, c=b,label=Betta  + x3.grd,y3.grd, c=green,label=Gamma
PLOT:	x1,c=b + y1,c=c
	x2 + y2,dms=6
	x3 + y3,ls=--,dlab="данные"
EoF

